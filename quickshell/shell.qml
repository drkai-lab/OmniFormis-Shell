//@ pragma UseQApplication
import QtQuick
import "."
import Quickshell
import Quickshell.Wayland
import Quickshell.Io
import Quickshell.Hyprland

ShellRoot {
    id: root

    property bool _initApp: {
        Qt.application.name = "quickshell";
        Qt.application.organization = "boing";
        Qt.application.domain = "boing.quickshell";
        return true;
    }

    property string globalOsIconPath: ""
    property var _forceInitLockScreen: LockScreen

    Process {
        id: osCheckProcess
        command: ["sh", "-c", "cat /etc/os-release | grep '^ID='"]
        running: true
        stdout: StdioCollector {
            onStreamFinished: {
                let text = this.text.trim();
                if (text.includes("nixos")) {
                    root.globalOsIconPath = "file:///home/boing/Dotfiles/quickshell/theme/assets/nixos-logo.png";
                } else if (text.includes("arch")) {
                    root.globalOsIconPath = "file:///home/boing/Dotfiles/quickshell/theme/assets/arch-logo.png";
                }
            }
        }
    }

    property bool launcherVisible: false
    property bool screenshotVisible: false
    property bool powerMenuVisible: false
    property bool overviewVisible: false
    property bool lensVisible: false

    onOverviewVisibleChanged: {
        if (overviewVisible) {
            topPills.closeAll();
            screenshotVisible = false;
        }
    }

    onScreenshotVisibleChanged: {
        if (screenshotVisible) {
            topPills.closeAll();
            overviewVisible = false;
            lensVisible = false;
        }
    }

    onLensVisibleChanged: {
        if (lensVisible) {
            topPills.closeAll();
            overviewVisible = false;
            screenshotVisible = false;
        }
    }

    // Global shortcuts route to TopPills
    GlobalShortcut {
        name: "launcher"
        description: "Toggle Launcher"
        onPressed: topPills.toggleLauncher()
    }

    GlobalShortcut {
        name: "control_center"
        description: "Toggle Control Center"
        onPressed: topPills.toggleControlCenter()
    }

    GlobalShortcut {
        name: "screenshot"
        description: "Toggle Screenshot"
        onPressed: root.screenshotVisible = !root.screenshotVisible
    }

    GlobalShortcut {
        name: "lens"
        description: "Circle to Search"
        onPressed: root.lensVisible = !root.lensVisible
    }

    GlobalShortcut {
        name: "power_menu"
        description: "Toggle Power Menu"
        onPressed: topPills.togglePowerMenu()
    }

    GlobalShortcut {
        name: "wallpaper"
        description: "Toggle Wallpaper Switcher"
        onPressed: topPills.toggleWallpaper()
    }

    GlobalShortcut {
        name: "color_scheme"
        description: "Toggle Color Scheme Switcher"
        onPressed: topPills.toggleColorScheme()
    }

    GlobalShortcut {
        name: "overview"
        description: "Toggle Overview"
        onPressed: root.overviewVisible = !root.overviewVisible
    }

    GlobalShortcut {
        name: "emoji_picker"
        description: "Toggle Emoji Picker"
        onPressed: topPills.toggleEmojiPicker()
    }

    GlobalShortcut {
        name: "clipboard"
        description: "Toggle Clipboard"
        onPressed: topPills.toggleClipboard()
    }

    ScreenShot {
        // Binding the internal visibility state to your root state variable
        visibleState: root.screenshotVisible || root.lensVisible
        isLensMode: root.lensVisible

        onScreenshotClosed: {
            root.screenshotVisible = false;
            root.lensVisible = false;
        }
    }

    ScreenFrame {
        id: screenFrame
    }

    WallpaperOverlay {
        // This will sit on the WlrLayer.Background
    }

    DesktopClock {
    }
    DesktopCalender {
    }
    DesktopMediaPlayer {
    }

    // Launcher is now inside TopPills.qml
    NotificationDaemon {
        id: notifDaemon
    }

    TopPills {
        id: topPills
        onToggleFloatingSettings: floatingSettings.toggle()
        onPopupOpened: {
            root.overviewVisible = false;
            root.screenshotVisible = false;
            root.lensVisible = false;
        }
        onOpenOverviewRequested: {
            root.overviewVisible = true;
        }
    }

    SettingsWindow {
        id: floatingSettings
        onRequestWidgetToggle: topPills.toggleSettings()
    }

    Overview {
        visibleState: root.overviewVisible
        onCloseRequested: {
            root.overviewVisible = false;
        }
    }
}
